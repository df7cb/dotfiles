#!/usr/bin/perl -w
# $Header$
# cb 030421: getopt support

#<map name="Testbild"> 
#<area shape=rect coords="1,1,249,49" href="#Anker"> 
#</map>
#<img src="hypgraph.gif" usemap="#Testbild" border=0>

$opt_w = 120;
$opt_h = 90;
$opt_b = 1;
$opt_n = 6;
$opt_m = 0;
$opt_f = "index";
$opt_H = "";
$opt_L = 1; # obsolete
$opt_q = 0;
$opt_s = 0;
$opt_t = "";
$opt_T = "";
$opt_v = 0;

use Getopt::Std;
getopts('w:h:H:b:n:mf:Lqst:T:v');

@ARGV = sort @ARGV if $opt_s;

@files = @ARGV;
@files = map {
	if($opt_t) {
		if(-f "$opt_t/$_") { # look for thumbs
			$_ = "$opt_t/$_";
		} elsif (/(.*)\.(gif|bmp)$/) {
			$_ = "$opt_t/$1.jpg" if -f "$opt_t/$1.jpg";
		}
	}
	s/\.gif$/.gif[0]/; # extract first frame from from animated gifs
	s/'/'\\''/g; # quote '
	$_ = "'$_'"; # quote filenames
} @files;
$files = join " ", @files;

print "Schreibe" unless $opt_q;
unless($opt_m) {
	$command =  "montage -geometry ${opt_w}x$opt_h+$opt_b+$opt_b -background white -tile ${opt_n}x100 -adjoin $files $opt_f.jpg";
	print "$command\n" if $opt_v;
	print " $opt_f.jpg..." unless $opt_q;
	system $command;
}

my $map = "$opt_f-map.html";
print " $map...\n" unless $opt_q;
open MAP, ">$map" or die "$map: $!";

my $name = $opt_f;
$name =~ s!.*/!!;

print MAP "<map name=\"$name\">\n";
$n = 0;
foreach (@ARGV) {
	$left = ($n % $opt_n) * ($opt_w+2*$opt_b);
	$right = $left + ($opt_w+$opt_b);
	$top = int($n / $opt_n) * ($opt_h+2*$opt_b);
	$bottom = $top + ($opt_h+$opt_b);
	my $href = $_;
	my $title = $_;
	if($opt_H) {
		eval "\$href =~ $opt_H";
	}
	if($opt_T) {
		eval "\$title =~ $opt_T";
	}
	print MAP " <area shape=rect coords=\"$left,$top,$right,$bottom\" href=\"$href\" alt=\"$title\" title=\"$title\">\n";
	$n++;
}
print MAP "</map>\n";
print MAP "<img src=\"$name.jpg\" usemap=\"#$name\" border=\"0\">\n" if $opt_L;
close MAP;


=head1 NAME

make-indexjpg - creates thumbnail image and corresponding HTML map file

=head1 SYNOPSIS

B<make-indexjpg>
[I<-w> B<x>]
[I<-h> B<y>]
[I<-b> B<b>]
[I<-n> B<n>]
[I<-m>]
[I<-f> B<name>]
[I<-H> B<expr>]
[I<-T> B<expr>]
[I<-q>]
[I<-s>]
[I<-t> B<directory>]
[I<-v>]
B<images> ...

=head1 DESCRIPTION

B<make-indexjpg> takes a list of image file names and uses B<montage> to create
an image with thumbnails. A correspoding HTML map file can be used to make the
index image clickable.

=head1 OPTIONS

=head2 I<-w> B<x> I<-h> B<y>

The thumbnails inside the index image will have size B<x> * B<y> pixels.
[120 * 90]

=head2 I<-b> B<b>

Put a border of B<b> pixels around each image. [1]

=head2 I<-n> B<n>

Arrange the thumbnails in a grid with B<n> columns. [6]

=head2 I<-m>

Only generate the B<name>-map.html file, omit B<name>.jpg. [generate both]

=head2 I<-f> B<name>

Name the index image B<name>.jpg and the HTML map file B<name>-map.html.
[index.jpg, index-map.html]

=head2 I<-H> B<expr>

Apply perl regexp to filenames before writing href attributes.
Example: -H 's/(.*)/images\/$1/'.
[do not]

=head2 I<-T> B<expr>

Apply perl regexp to filenames before writing alt and title attributes.
Example: -T 's/(.*)\.jpg/Picture of $1/'.
[do not]

=head2 I<-q>

Be quiet. [print names of files written]

=head2 I<-s>

Sort the list of images given. [do not]

=head2 I<-t> B<directory>

Look for precomputed thumbnails in B<directory>. Use this to speed up the
B<montage> call. [no thumbnails]

=head2 I<-v>

Be verbose, print commands as they are executed. [do not]

=head1 AUTHOR

Christoph Berg <cb@cs.uni-sb.de>
